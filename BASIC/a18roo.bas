5 PRINT "ENHROO AUG 30 76"
10 PRINT "GRID SEARCH"
20 READ U
30 REM PRINT "U=";U
40 READ V
50 PRINT "U=";U;"  V=";V
70 READ N9
80 PRINT "# OF POINTS";N9
100 LET H=(V-U)/N9
110 FOR I=0 TO N9
120 LET B=U+I*H
130 GOSUB 2000
140 PRINT "F(",B,")=",P
150 NEXT I
160 REM STOP 
200 PRINT "ROOTFINDER"
210 READ U
220 REM PRINT "U=";U
230 READ V
240 PRINT "U=";U;"  V=";V
250 REM PRINT 
260 READ N9
270 PRINT "BISECTION EVERY";N9
280 REM PRINT 
290 READ E3
300 PRINT "TOLERANCE";E3
310 REM PRINT 
320 GOSUB 1000
330 PRINT "ROOT: F(",B,")=",P
335 PRINT "Done!" : rem stop 
340 QUIT
1000 REM BISECTION/FALSE POSITION ROOT-FINDER
1010 LET B=U
1020 GOSUB 2000
1030 LET F1=P
1040 LET B=V
1050 GOSUB 2000
1060 LET F2=P
1070 IF F1*F2<=0 THEN GOTO 1090
1075 PRINT "FUNCTIONS HAVE SAME SIGN AT BOTH ENDS OF INTERVAL"
1080 QUIT
1090 PRINT "F(";U;")=";F1;" F(";V;")=";F2
1100 LET I9=0
1110 REM FALSE POSITION
1115 PRINT "FP ";
1120 LET B=(U*F2-V*F1)/(F2-F1)
1130 IF B>U THEN GOTO 1160
1140 LET B=U
1145 LET P=F1
1150 GOTO 1320
1160 IF B<V THEN GOTO 1190
1170 LET B=V
1175 LET P=F2
1180 GOTO 1320
1190 LET I9=I9+1
1200 GOSUB 2000
1210 PRINT "ITN";I9;" U=";U;" V=";V;" F(";B;")=";P
1220 IF P*F1>0 THEN GOTO 1260
1230 LET F2=P
1240 LET V=B
1250 GOTO 1280
1260 LET F1=P
1270 LET U=B
1280 IF (V-U)<E3 THEN GOTO 1320
1290 IF N9*INT(I9/N9)<>I9 THEN GOTO 1110
1295 PRINT "BI ";
1300 LET B=(U+V)/2 : REM BETTER IS U+(V-U)*O.5
1310 GOTO 1130
1320 PRINT "CONVERGED"
1330 RETURN 
2000 REM CUBIC FUNCTION TEST
2010 LET P=B*(B*B-2.0)-5.0
2020 REM NOTE USE ARGUMENT B AND RETURNED VALUE P
2090 RETURN 
2200 DATA  0, 5, 10, 2, 2.5, 5, 1e-12
2300 END 
