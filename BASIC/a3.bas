2 REM DIM A(10,10),Q(10,10)
10 PRINT "TEST GIVENS - GIFT - ALG 3"
12 LET M8=10
14 LET N8=10
20 DIM A(M8,N8),Q(M8,M8)
25 REM PRINT "M=",
30 REM INPUT M
32 LET M=5
40 REM PRINT "  N=",
50 REM INPUT N
52 LET N=3
70 GOSUB 1500
80 PRINT "ORIGINAL",
85 GOSUB 790
90 GOSUB 500 : REM GIVENS DECOMPOSITION
94 PRINT "FINAL ";
96 GOSUB 790
97 PRINT "FINAL ";
98 GOSUB 840
100 PRINT "RECOMBINATION "
110 FOR I=1 TO M
111   PRINT "ROW";I;":";
120   FOR J=1 TO N
130     LET S=0
140     FOR K=1 TO M
150       LET S=S+Q(I,K)*A(K,J)
160     NEXT K
170     PRINT S;" ";
210   NEXT J
220   PRINT 
230 NEXT I
240 QUIT
245 REM STOP 
500 REM GIVENS TRIANGULARIZATION
520 PRINT "GIVENS TRIANGULARIZATION DEC 12 77"
540 FOR I=1 TO M
545   FOR J=1 TO M
550     LET Q(I,J)=0
555   NEXT J
560   LET Q(I,I)=1
565 NEXT I
575 REM GOSUB 840: REM PRINT ORIGINAL Q MATRIX
580 LET E1=1E-7 : REM NORTH STAR 8 DIGIT -- can be changed!
585 LET T9=E1*E1
600 FOR J=1 TO N-1
605   FOR K=J+1 TO M
610     LET C=A(J,J)
615     LET S=A(K,J)
625     REM PRINT "J=",J,"  K=",K,"  A[J,J]=",C,"  A[K,J]=",S
630     REM PRINT "BYPASS SAFETY DIVISION  ",
635     REM GOTO 660
640     LET B=ABS(C)
645     IF ABS(S)<=B THEN GOTO 655
650     LET B=ABS(S)
655     LET C=C/B
660     LET S=S/B
665     IF B=0 THEN GOTO 770
670     LET P=SQR(C*C+S*S)
680     LET S=S/P
685     IF ABS(S)<T9 THEN GOTO 770
690     LET C=C/P
695     FOR I=1 TO N
700       LET P=A(J,I)
705       LET A(J,I)=C*P+S*A(K,I)
710       LET A(K,I)=-S*P+C*A(K,I)
715      NEXT I
720     IF J=N-1 THEN GOTO 730
730     REM IF I5=0 THEN GOTO 770
735     FOR I=1 TO M
740       LET P=Q(I,J)
745       LET Q(I,J)=C*P+S*Q(I,K)
750       LET Q(I,K)=-S*P+C*Q(I,K)
755     NEXT I
770     REM Possible print point
775   NEXT K
780 NEXT J
785 RETURN 
790 PRINT "  A MATRIX"
795 FOR H=1 TO M
800   PRINT "ROW";H;":";
805   FOR L=1 TO N
810      PRINT A(H,L);" ";
815   NEXT L
820   PRINT 
825 NEXT H
830 PRINT 
835 RETURN 
840 PRINT "  Q MATRIX"
845 FOR H=1 TO M
850   PRINT "ROW";H;":";
855   FOR L=1 TO M
860     PRINT Q(H,L);" ";
865   NEXT L
870   PRINT 
875 NEXT H
880 PRINT 
885 RETURN 
1500 REM PREPARE FRANK MATRIX IN A
1510 FOR I=1 TO M
1530 FOR J=1 TO N
1540 IF (I <= J) THEN LET A(I,J)=I ELSE LET A(I,J)=J
1550 NEXT J
1560 NEXT I
1570 RETURN
1600 END
