10 PRINT "A14 JACOBI EIGENSOLUTIONS OF A SYMMETRIC MATRIX VIA SVD"
20 LET N9=20
30 DIM A(N9,N9),V(N9,N9),B(N9,N9),D(N9),Z(N9),W(N9) 
40 PRINT "ORDER OF MATRIX: ";
50 READ N
55 PRINT N
60 IF (N <= 0) THEN QUIT
70 LET M=N
80 GOSUB 1500: REM BUILD FRANK MATRIX N BY N
90 gosub 700: print A and B
100 GOSUB 3000: REM JACOBI ROUTINE
105 GOSUB 800: REM PRINT SOLUTIONS
110 FOR I=1 TO N
113 print "compute residual and RQ for soln ",I
115   GOSUB 400 : REM COMPUTE RAYLEIGH QUOTIENT AND RESIDUAL FOR I
120   PRINT "EV(";I;")=";D(I);"    RQ=";Z(I)
125   FOR J=1 TO N
130      PRINT V(J,I);" ";
135   NEXT J
160   PRINT " MAX ABS RESIDUAL=";R9
165 NEXT I
170 PRINT
200 GOTO 40
300 DATA 3, 0 :REM 2, 4, 10, 1, 0
400 LET S1=0: REM RAYLEIGH QUOTIENT FOR V(.,I)
405 FOR K=1 TO N
410   LET T1=0
415   FOR J=1 TO N
420     LET T1=T1+B(K,J)*V(J,I)
425   NEXT J
430   W(K)=T1: REM A*V ELEMENT K
432   print I;K;T1
435   IF (I = K) THEN LET T1=T1-D(I)*V(I,I)
437   IF (ABS(T1) > R9) THEN LET R9=ABS(T1)
438 NEXT K
440 T1=0
445 FOR J=1 TO N
450   LET T1=T1+V(J,I)*V(J,I): REM NORMALIZATION?
455   LET S1=S1+V(J,I)*W(J)
460 NEXT J
462 REM print "S1, T1:";S1;T1
465 LET Z(I)=S1/T1
470 RETURN
700 print "A and B:"
705 FOR I=1 TO N
710 FOR J=1 TO N 
715 print A(I,J);
720 NEXT J
725 PRINT
730 NEXT I
740 RETURN
800 PRINT "D, V:"
805 FOR I=1 TO N
807 PRINT D(I);":";
810 FOR J=1 TO N 
815 print V(I,J);
820 NEXT J
825 PRINT
830 NEXT I
840 RETURN
1500 REM PREPARE FRANK MATRIX IN A
1510 FOR I=1 TO M
1530 FOR J=1 TO N
1540 IF (I <= J) THEN LET A(I,J)=I ELSE LET A(I,J)=J
1545 LET B(I,J)=A(I,J)
1550 NEXT J
1560 NEXT I
1570 RETURN
3000 PRINT "SMEV.JC ALG 14 DEC 7 78"
3005 REM DIM A(N,N),V(N,N)
3010 FOR I=1 TO N
3015   FOR J=1 TO N
3020     LET V(I,J)=0
3025   NEXT J
3030   LET V(I,I)=1
3035 NEXT I
3040 LET I9=0
3045 LET I9=I9+1
3050 IF I9>30 THEN GOTO 3240: REM CHOICE?
3055 LET N8=0
3060 FOR I=1 TO N-1
3065   FOR J=I+1 TO N
3070     LET P=A(I,J)+A(J,I): REM ENSURE SYMMETRY?
3075     LET Q=A(I,I)-A(J,J)
3080     LET T=SQR(P*P+Q*Q)
3085     IF T=0 THEN GOTO 3145
3090     IF Q<0 THEN GOTO 3120
3095     IF ABS(A(I,I))<ABS(A(I,I))+50*ABS(P) THEN GOTO 3105
3100     IF ABS(A(J,J))=ABS(A(J,J))+50*ABS(P) THEN GOTO 3145
3105     LET C=SQR((T+Q)/(2*T))
3110     LET S=.5*P/(T*C)
3115     GOTO 3140
3120     LET S=SQR((T-Q)/(2*T))
3125     IF P>0 THEN GOTO 3135
3130     LET S=-S
3135     LET C=.5*P/(T*S)
3140     IF 1<1+ABS(S) THEN GOTO 3155
3145     LET N8=N8+1
3150     GOTO 3220
3155     FOR K=1 TO N
3160       LET Q=A(I,K)
3165       LET A(I,K)=C*Q+S*A(J,K)
3170       LET A(J,K)=-S*Q+C*A(J,K)
3175     NEXT K
3180     FOR K=1 TO N
3185       LET Q=A(K,I)
3190       LET A(K,I)=C*Q+S*A(K,J)
3195       LET A(K,J)=-S*Q+C*A(K,J)
3200       LET Q=V(K,I)
3205       LET V(K,I)=C*Q+S*V(K,J)
3210       LET V(K,J)=-S*Q+C*V(K,J)
3215     NEXT K
3220   NEXT J
3225 NEXT I
3230 PRINT N8," SMALL ROTNS -- SWEEP",I9
3235 IF N8<N*(N-1)/2 THEN GOTO 3045
3240 PRINT "CONVERGED"
3245 FOR I=1 TO N
3250   LET D(I)=A(I,I)
3255 NEXT I
3260 RETURN 
