C&&& A1112
C  TEST ALGORITHMS 11 & 12
C  J.C. NASH   JULY 1978, APRIL 1989
      INTEGER N,NS,NIN,NOUT
      REAL A(10,10),Z(10,10),X(10),Y(10),U(10),V(10),E,F,VNORM
      NA=10
      NIN=5
      NOUT=6
  10  READ(NIN,905)N
 905  FORMAT(I5)
      WRITE(NOUT,965)N
 965  FORMAT(' ORDER=',I4)
      IF(N.LE.0)STOP
C  READ MATRICES
      DO 20 I=1,N
        READ(NIN,906)(A(I,J),J=1,N)
 906    FORMAT(6F10.5)
  20  CONTINUE
      WRITE(NOUT,966)
 966  FORMAT(' MATRIX A')
      CALL OUT(A,NA,N,N,NOUT)
      DO 30 I=1,N
        READ(NIN,906)(Z(I,J),J=1,N)
  30  CONTINUE
      WRITE(NOUT,967)
 967  FORMAT(' MATRIX Z')
      CALL OUT(Z,NA,N,N,NOUT)
  40  READ(NIN,907)NS,E,F
 907  FORMAT(I5,5X,2F10.5)
      WRITE(NOUT,968)NS,E,F
 968  FORMAT(' SOLUTION',I4,'  EV=',1PE16.8,' + SQRT(-1)*',E16.8)
      IF(NS.LE.0)GOTO 10
      READ(NIN,906)(X(J),J=1,N)
      WRITE(NOUT,969)
 969  FORMAT(' REAL PART')
      WRITE(NOUT,970)(X(J),J=1,N)
 970  FORMAT(1H ,6E16.8)
      READ(NIN,906)(Y(J),J=1,N)
      WRITE(NOUT,971)
 971  FORMAT(' IMAGINARY PART')
      WRITE(NOUT,970)(Y(J),J=1,N)
      CALL A11VS(N,X,Y,VNORM)
      WRITE(NOUT,972)VNORM
 972  FORMAT(' STANDARDIZED VECTOR - NORM=',1PE16.8)
      WRITE(NOUT,969)
      WRITE(NOUT,970)(X(J),J=1,N)
      WRITE(NOUT,971)
      WRITE(NOUT,970)(Y(J),J=1,N)
      WRITE(NOUT,973)
 973  FORMAT(' RESIDUALS')
      CALL A12CVR(N,X,Y,A,10,Z,10,E,F,U,V)
      WRITE(NOUT,969)
      WRITE(NOUT,970)(U(J),J=1,N)
      WRITE(NOUT,971)
      WRITE(NOUT,970)(V(J),J=1,N)
      GOTO 40
      END
      SUBROUTINE A11VS(N,X,Y,VNORM)
C  ALGORITHM 11  -  COMPLEX VECTOR STANDARDISATION
C  J.C. NASH   JULY 1978, FEBRUARY 1980, APRIL 1989
C  STANDARDISES COMPEX VECTOR (N ELEMENTS)  X+SQRT(-1)*Y
C      TO  1.0+SQRT(-1)*0.0
C  VNORM = NORM OF VECTOR (LARGEST ELEMENT)
C  STEP 0
      INTEGER N,I,K
      REAL X(N),Y(N),VNORM,G,B,E,S
C  STEP 1
      G=0.0
C  STEP 2
      DO 60 I=1,N
C  STEP 3
        B=X(I)**2+Y(I)**2
C  STEP 4
        IF(B.LE.G)GOTO 60
C  STEP 5
        K=I
        G=B
C  STEP 6
  60  CONTINUE
C  SAVE NORM
      VNORM=G
C  SAFETY CHECK
      IF(G.EQ.0.0)RETURN
C  STEP 7
      E=X(K)/G
      S=-Y(K)/G
C  STEP 8
      DO 85 I=1,N
        G=X(I)*E-Y(I)*S
        Y(I)=Y(I)*E+X(I)*S
        X(I)=G
  85  CONTINUE
C  END
      RETURN
      END
      SUBROUTINE A12CVR(N,X,Y,A,NA,Z,NZ,E,F,U,V)
C  ALGORITHM 12 RESIDUALS OF A COMPLEX EIGENSOLUTION
C  J.C. NASH   JULY 1978, FEBRUARY 1980, APRIL 1989
C  N    =  ORDER OF PROBLEM
C  U + I*V =  ( A + I*Z - E - I*F)*(X + I*Y)  WHERE I=SQRT(-1)
C  NA,NZ = FIRST DIMENSIONS OF A & Z RESPECTIVELY
C  STEP 0
      INTEGER N,NA,NZ,J,K
      REAL A(NA,N),Z(NZ,N),X(N),Y(N),E,F,U(N),V(N),S,G
C  STEP 1
      DO 50 J=1,N
C  STEP 2
        S=-E*X(J)+F*Y(J)
        G=-F*X(J)-E*Y(J)
C  STEP 3
        DO 35 K=1,N
          S=S+A(J,K)*X(K)-Z(J,K)*Y(K)
          G=G+A(J,K)*Y(K)+Z(J,K)*X(K)
  35    CONTINUE
C  STEP 4  NOTE SAVE IN U & V
        U(J)=S
        V(J)=G
C  STEP 5
  50  CONTINUE
      RETURN
      END
      SUBROUTINE OUT(A,NA,N,NP,NOUT)
C  J.C. NASH   JULY 1978, APRIL 1989
      INTEGER NA,N,NOUT,I,J
      REAL A(NA,N)
      DO 20 I=1,N
        WRITE(NOUT,951)I
 951    FORMAT(' ROW',I3)
        WRITE(NOUT,952)(A(I,J),J=1,NP)
 952    FORMAT(1H ,1P5E16.8)
  20  CONTINUE
      RETURN
      END
