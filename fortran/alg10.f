      SUBROUTINE A10GII(W,NW,N,N2,X,Y,SHIFT,EPS,LIMIT,EV,IPR)
C  ALGORITHM 10
C  J.C. NASH   JULY 1978, FEBRUARY 1980, APRIL 1989
C  INVERSE ITERATION VIA GAUSS ELIMINATION
C  SOLVES EIGENPROBLEM A*X=EV*B*X FOR EIGENSOLUTION (EX,X)
C  VECTOR NORMALISED SO LARGEST ELEMENT IS 1.0
C  W=WORKING ARRAY HAVING INITIALLY A IN COLUMNS 1 TO N
C                                   B IN COLUMNS N+1 TO 2*N=N2
C  NW=FIRST DIMENSION OF W
C  N=ORDER OF PROBLEM
C  N2=2*N = NO. OF COLUMNS IN W
C  X = INITIAL GUESS FOR EIGENVECTOR - SHOULD NOT BE NULL
C  Y = WORKING VECTOR
C  X & Y OF LENGTH N AT LEAST
C  SHIFT = SHIFT TO TRANSFORM PROBLEM TO ONE WITH EV CLOSEST TO SHIFT
C    WITH EV CLOSEST TO SHIFT
C  EPS=MACHINE PRECISION--SMALLEST NUMBER S.T. 1.0+EPS.GT.1.0
C  LIMIT=UPPER BOUND TO NUMBER OF ITERATIONS
C       = ON OUTPUT THE NUMBER OF ITERATIONS USED
C  EV=EIGENVALUE CALCULATED
C  IPR=PRINT CHANNEL. IPR=0 SUPPRESSES PRINTING.
C  STEP 0
      INTEGER N,N2,NW,LIMIT,COUNT,I,J,JN,K,N1,I1
      REAL W(NW,N2),X(N),Y(N),EPS,SHIFT,EV,S,T,P
C  SAFETY CHECK
      IF(N2.NE.2*N)STOP
C  STEP 1
      T=0.0
      DO 10 I=1,N
        Y(I)=0.0
        S=0.0
        DO 5 J=1,N
          JN=J+N
          W(I,J)=W(I,J)-SHIFT*W(I,JN)
          S=S+ABS(W(I,J))
   5    CONTINUE
        IF(T.LT.S)T=S
  10  CONTINUE
      T=T*EPS
C  STEP 2
      N1=N-1
      DO 100 I=1,N1
C  STEP 3
        S=ABS(W(I,I))
        K=I
        I1=I+1
        DO 20 J=I1,N
          IF(ABS(W(J,I)).LE.S)GOTO 20
          S=ABS(W(J,I))
          K=J
  20    CONTINUE
      IF(S.GT.0.0)GOTO 30
C  STEP 4
        W(I,I)=T
        GOTO 100
C  STEP 5
  30    IF(K.EQ.I)GOTO 50
C  STEP 6
        DO 40 J=I,N2
          S=W(I,J)
          W(I,J)=W(K,J)
          W(K,J)=S
  40    CONTINUE
C  STEP 7
  50    DO 80 J=I1,N
          S=W(J,I)/W(I,I)
          DO 70 K=I,N2
            W(J,K)=W(J,K)-S*W(I,K)
  70      CONTINUE
  80    CONTINUE
C  STEP 8
 100  CONTINUE
C  STEP 9
      IF(ABS(W(N,N)).EQ.0.0)W(N,N)=T
C  STEP 10
      COUNT=0
C  STEP 11
 110  COUNT=COUNT+1
      M=N
      S=X(N)
      X(N)=Y(N)
      Y(N)=S/W(N,N)
      P=ABS(Y(N))
C  STEP 12
      DO 130 JN=1,N1
        I=N-JN
        S=X(I)
        X(I)=Y(I)
        I1=I+1
        DO 120 J=I1,N
          S=S-W(I,J)*Y(J)
 120    CONTINUE
        Y(I)=S/W(I,I)
        IF(ABS(Y(I)).LE.P)GOTO 130
        P=ABS(Y(I))
        M=I
 130  CONTINUE
C  STEP 13
      EV=SHIFT+X(M)/Y(M)
C  STEP 14
      P=Y(M)
      M=0
      DO 140 I=1,N
        Y(I)=Y(I)/P
        IF(FLOAT(N)+Y(I).EQ.FLOAT(N)+X(I))M=M+1
 140  CONTINUE
      IF(IPR.GT.0)WRITE(IPR,960)COUNT,EV,M
 960  FORMAT(14H ITERATION NO.,I4,14H   APPROX. EV=,1PE16.8,5X,I4,
     *27H VECTOR ELEMENTS TEST EQUAL)
C  STEP 15
      IF(M.EQ.N)GOTO 200
      IF(COUNT.GT.LIMIT)GOTO 200
C  STEP 16
      DO 160 I=1,N
        S=0.0
        DO 150 J=1,N
          JN=J+N
          S=S+W(I,JN)*Y(J)
 150    CONTINUE
        X(I)=S
 160  CONTINUE
C  STEP 17
      GOTO 110
 200  LIMIT=COUNT
      RETURN
      END
