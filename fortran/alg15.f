      SUBROUTINE A15GSE(N,A,NA,B,NB,V,NV,FAIL,ISWP,IPR)
C  ALGORITHM 15 GENERALISED SYMMETRIC EIGENPROBLEM BY 2 APPLICATIONS
C    OF JACOBI ALGORITHM 14
C  J.C. NASH   JULY 1978, FEBRUARY 1980, APRIL 1989
C  N     =   ORDER OF PROBLEM
C  A     =   A  MATRIX OF EIGENPROBLEM. DIAGONAL ELEMENTS BECOME
C  NA    =   FIRST DIMENSION OF A
C  B     =   B  MATRIX OF EIGENPROBLEM, MUST BE POSITIVE DEFINITE
C  NB    =   FIRST DIMENSION OF B
C  V     =   VECTOR MATRIX. ON OUTPUT COLUMNS ARE EIGENVECTORS
C            EIGENVALUES
C  FAIL  =   LOGICAL FLAG SET .TRUE. IF B NOT COMPUTATIONALLY
C              POSITIVE DEFINITE OR IF EITHER APPLICATION OF
C              ALGORITHM 14 TAKES MORE THAN ISWP SWEEPS
C  NV    =   FIRST DIMENSION OF V
C  ISWP  =   BOUND ON SWEEPS IN ALG. 14.
C  NA,NB,NV ALL .GE. N
C  IPR   =   PRINT CHANNEL.  IPR.GT.0 FOR PRINTING
C  STEP 0
      LOGICAL FAIL,COMV,SETV
      INTEGER N,NA,NB,NV,STAGE,ISWP,LISWP,I,J,K,M,IPR
      REAL A(NA,N),B(NB,N),V(NV,N),S
      FAIL=.FALSE.
      STAGE=1
      SETV=.TRUE.
      COMV=.TRUE.
C  STEP 1  INTERCHANGE - NOT GENERALLY EFFICIENT
  10  DO 16 I=1,N
        DO 14 J=1,N
          S=A(I,J)
          A(I,J)=B(I,J)
          B(I,J)=S
  14    CONTINUE
  16  CONTINUE
C  STEP 2
      LISWP=ISWP
      IF(IPR.GT.0)WRITE(IPR,964)STAGE
 964  FORMAT( 6H STAGE,I3)
      CALL A14JE(N,A,NA,V,NV,LISWP,IPR,SETV,COMV)
      IF(LISWP.GE.ISWP)FAIL=.TRUE.
      IF(FAIL)RETURN
C  STEP 3
      IF(STAGE.EQ.2)GOTO 80
C  STEP 4
      STAGE=2
      SETV=.FALSE.
      DO 46 I=1,N
        IF(A(I,I).LE.0.0)FAIL=.TRUE.
        IF(FAIL)RETURN
        S=1.0/SQRT(A(I,I))
        DO 44 J=1,N
          V(J,I)=S*V(J,I)
  44    CONTINUE
  46  CONTINUE
C  STEP 5
      DO 56 I=1,N
        DO 54 J=1,N
          A(I,J)=B(I,J)
  54    CONTINUE
  56  CONTINUE
C  STEP 6
      DO 68 I=1,N
        DO 66 J=I,N
          S=0.0
          DO 64 K=1,N
            DO 62 M=1,N
              S=S+V(K,I)*A(K,M)*V(M,J)
  62        CONTINUE
  64      CONTINUE
          B(I,J)=S
          B(J,I)=S
  66    CONTINUE
  68  CONTINUE
C  STEP 7
      GOTO 10
  80  ISWP=0
      RETURN
      END

