C&&& A5-6
C  TEST ALGORITHMS 5 & 6 -- A5GE + A6BS
C  J.C. NASH   JULY 1978, APRIL 1989
C  USES FRANK MATRIX - STABLE TEST
      INTEGER N,NA,NP,I,J,NOUT
      REAL A(10,11),TOL
      NA=10
C  PRINT CHANNEL
      NOUT=6
C  OUTER LOOP
      DO 100 N=4,8,4
C  CREATE A
        DO 10 I=1,N
          DO 5 J=I,N
            A(I,J)=I
            A(J,I)=I
C  Fortran 2018 common end to multiple DO not permitted
   5      CONTINUE
  10    CONTINUE
        DO 20 I=1,N
          S=0.0
C  SOLUTION ALL 1.0 ELEMENTS
        NP=N+1
          DO 15 J=1,N
            S=S+A(I,J)
  15      CONTINUE
        A(I,NP)=S
  20  CONTINUE
C    TOL IS VERY SMALL NUMBER TOLERANCE
        TOL=1.0E-15
C    TOL HERE ENTERED AS A CONSTANT, BUT COULD BE OBTAINED
C       FROM SOME FORM OF STANDARD MACHINE ENVIRONMENT ENQUIRY
C&&&         TOL=R1MACH(4)
        WRITE(NOUT,950)N
 950    FORMAT(' ORDER=',I4,'  ORIGINAL MATRIX WITH RHS APPENDED')
        CALL OUT(A,NA,N,NP,NOUT)
        CALL A5GE(A,NA,N,NP,D,TOL)
      WRITE(NOUT,963)D
 963  FORMAT(' DETERMINANT=',1PE12.5)
C       WRITE(NOUT,953)
C953    FORMAT(' DECOMPOSED MATRIX')
C       CALL OUT(A,NA,N,NP,NOUT)
      CALL A6BS(A,10,N,NP)
C     WRITE(NOUT,954)
C954    FORMAT(' FINAL MATRIX')
C       CALL OUT(A,NA,N,NP,NOUT)
        DO 30 I=1,N
          S=A(I,NP)-1.0
          WRITE(NOUT,955)I,A(I,NP),S
 955      FORMAT(' SOLN X(',I3,')=',1PE12.5,'   ERROR=',0PE12.5)
  30    CONTINUE
 100  CONTINUE
      STOP
      END
      SUBROUTINE OUT(A,NA,N,NP,NOUT)
C  J.C. NASH   JULY 1978, APRIL 1989
      INTEGER NA,N,NOUT,I,J
      REAL A(NA,N)
      DO 20 I=1,N
        WRITE(NOUT,951)I
 951    FORMAT(' ROW',I3)
        WRITE(NOUT,952)(A(I,J),J=1,NP)
 952    FORMAT(1H ,1P5E12.5)
  20  CONTINUE
      RETURN
      END
      SUBROUTINE A5GE(A,NA,N,NP,D,TOL)
C   ALGORITHM 5
C  J.C. NASH   JULY 1978, FEBRUARY 1980, APRIL 1989
C   GAUSS ELIMINATION WITH PARTIAL PIVOTING
C     A=WORKING ARRAY -- COLUMNS 1 TO N HAVE COEFFICIENT MATRIX
C                     -- COLUMNS N+1 TO NP HAVE RIGHT HAND SIDES
C     NA=FIRST DIMENSION OF A .GE. N
C     N=ORDER OF EQUATIONS
C     NP=N + NO. OF RIGHT HAND SIDES
C     D=DETERMINANT OF COEFFICIENT MATRIX (OUTPUT ONLY)
C     TOL=TOLERANCE FOR ZERO SCALED TO SIZE OF COEFFICINT ELEMENTS
C        TOL IS SET NEGATIVE IF COEFFICIENT MATRIX COMPUTATIONALLY
C        SINGULAR. NEGATIVE TOL ON INPUT STOPS EXECUTION
C  STEP 0
      INTEGER N,NA,NP,I,N1,J,H,K,J1
      REAL D,TOL,A(NA,NP),S
      D=1.0
      IF(TOL.LE.0.0)STOP
C  STEP 1
      N1=N-1
      DO 60 J=1,N1
C  STEP 2A
        S=ABS(A(J,J))
        K=J
C  STEP 2B
        J1=J+1
        DO 10 H=J1,N
          IF(ABS(A(H,J)).LE.S)GOTO 10
          S=ABS(A(H,J))
          K=H
  10    CONTINUE
        IF(K.EQ.J)GOTO 30
C  STEP 3
        DO 20 I=J,NP
          S=A(K,I)
          A(K,I)=A(J,I)
          A(J,I)=S
  20    CONTINUE
        D=-D
C  STEP 4
  30    D=D*A(J,J)
        IF(ABS(A(J,J)).LE.TOL)GOTO 70
C  STEP 5
        DO 50 K=J1,N
          A(K,J)=A(K,J)/A(J,J)
          DO 40 I=J1,NP
            A(K,I)=A(K,I)-A(K,J)*A(J,I)
  40      CONTINUE
  50    CONTINUE
C  STEP 6
  60  CONTINUE
C  STEP 7
      D=D*A(N,N)
      IF(ABS(A(N,N)).LE.TOL)GOTO 70
      RETURN
C  COMPUTATIONALLY SINGULAR COEFFICIENT MATRIX -- EXIT WITH TOL.LT.0.0
  70  TOL=-1.0
      RETURN
      END
      SUBROUTINE A6BS(A,NA,N,NP)
C  ALGORITHM 6
C  J.C. NASH   JULY 1978, APRIL 1989
C  BACK-SUBSTITUTION TO FOLLOW GAUSS ELIMINATION
C     A=WORKING ARRAY AS OUTPUT BY A5GE - ALGORITHM 5
C     NA=FIRST DIMENSION OF A
C     NP=N + NO. OF (TRANSFORMED) RIGHT HAND SIDES
C     N=ORDER OF EQUATIONS
C  STEP 0
      INTEGER N,NA,NP,N1,I,J,JJ,K
      REAL A(NA,NP),S
C  STEP 1
      N1=N+1
      DO 200 I=N1,NP
C  STEP 2
        A(N,I)=A(N,I)/A(N,N)
        IF(N.EQ.1)GOTO 200
C  STEP 3 - NOTE FORM FOR LOOPING
        DO 180 JJ=2,N
          J=N1-JJ
C  STEP 4
          S=A(J,I)
C  STEP 5
          J1=J+1
          DO 160 K=J1,N
            S=S-A(J,K)*A(K,I)
 160      CONTINUE
C  STEP 6
          A(J,I)=S/A(J,J)
C  STEP 7
 180    CONTINUE
C  STEP 8
 200  CONTINUE
      RETURN
      END
