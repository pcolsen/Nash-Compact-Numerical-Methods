5 PRINT "dr0102.bas -- Nashlib Alg 01 and 02 driver"
6 REM Some issues are because Data General BASIC only
7 REM allowed A-Z variable names!
10 PRINT "from ENHSVA APR 7 80 -- MOD 850519, remod 210113"
20 LET E1=1.0E-7 
30 PRINT "ONE SIDED TRANSFORMATION METHOD FOR REGRESSIONS VIA"
40 PRINT "THE SINGULAR VALUE DECOMPOSITION -- J.C.NASH 1973,79"
150 LET M=4
160 LET N=3
200 REM RATIONALIZE STORAGE??
210 DIM Y(M,N+1),A(M,N),T(N,N),G(N),X(N),Z(N),U(N),B(M)
236 REM PRINT "Prep matrix and RHS"
237 REM This is a VERY pedestrian way to do this.
239 PRINT "Matrix A:"
240 LET Y(1,1)=5
241 LET Y(1,2)=1.0E-6
242 LET Y(1,3)=1
243 LET B(1)=1
250 LET Y(2,1)=6
251 LET Y(2,2)=0.999999
252 LET Y(2,3)=1
253 LET B(2)=2
260 LET Y(3,1)=7
261 LET Y(3,2)=2.00001
262 LET Y(3,3)=1
263 LET B(3)=3
270 LET Y(4,1)=8
271 LET Y(4,2)=2.9999
272 LET Y(4,3)=1
273 LET B(4)=4
500 FOR I=1 TO M
510 FOR J=1 TO N
520 LET A(I,J)=Y(I,J)
525 PRINT A(I,J);
530 NEXT J
535 PRINT
550 NEXT I
560 LET E2=N*N*E1*E1
570 PRINT 
580 FOR I=1 TO N
590 FOR J=1 TO N
600 LET T(I,J)=0
610 NEXT J
620 LET T(I,I)=1
630 NEXT I
640 LET I9=0
650 IF N=1 THEN GOTO 1150
660 LET N2=N*(N-1)/2
670 LET N1=N-1
680 LET N9=N2
690 LET I9=I9+1
700 FOR J=1 TO N1
710 LET J1=J+1
720 FOR K=J1 TO N
730 LET P=0
740 LET Q=0
750 LET R=0
760 FOR I=1 TO M
770 LET P=P+A(I,J)*A(I,K)
780 LET Q=Q+A(I,J)*A(I,J)
790 LET R=R+A(I,K)*A(I,K)
800 NEXT I
810 IF Q>=R THEN GOTO 850
820 LET C=0
830 LET S=1
840 GOTO 920
850 IF (Q*R)<=0 THEN GOTO 1040
860 IF P*P/(Q*R)<E2 THEN GOTO 1040
870 LET Q=Q-R
880 LET P=2*P
890 LET V1=SQR(P*P+Q*Q)
900 LET C=SQR((V1+Q)/(2*V1))
910 LET S=P/(2*V1*C)
920 FOR I=1 TO M
930 LET V1=A(I,J)
940 LET A(I,J)=V1*C+A(I,K)*S
950 LET A(I,K)=-V1*S+A(I,K)*C
960 NEXT I
970 FOR I=1 TO N
980 LET V1=T(I,J)
990 LET T(I,J)=V1*C+T(I,K)*S
1000 LET T(I,K)=-V1*S+T(I,K)*C
1010 NEXT I
1020 LET N9=N2
1030 GOTO 1060
1040 LET N9=N9-1
1050 IF N9=0 THEN GOTO 1150
1051 REM ?? GOTO was EXIT for NS BASIC
1060 NEXT K
1070 NEXT J
1080 PRINT "SWEEP",I9,
1090 IF O1>0 THEN PRINT #O1,"SWEEP ",I9," ",
1100 IF 6*INT(I9/6)<>I9 THEN GOTO 680
1110 IF O1>0 THEN PRINT #O1
1120 IF I9>=30 THEN GOTO 1150
1130 PRINT 
1140 GOTO 680
1150 PRINT 
1160 IF O1>0 THEN PRINT #O1
1170 PRINT "CONVERGENCE AT SWEEP ",I9
1180 IF O1>0 THEN PRINT #O1,"CONVERGENCE AT SWEEP ",I9
1190 FOR J=1 TO N
1200 LET Q=0
1210 FOR I=1 TO M
1220 LET Q=Q+A(I,J)^2
1230 NEXT I
1240 LET Q=SQR(Q)
1250 IF Q=0 THEN GOTO 1290
1260 FOR I=1 TO M
1270 LET A(I,J)=A(I,J)/Q
1280 NEXT I
1290 LET Z(J)=Q
1300 NEXT J
1310 PRINT 
1320 PRINT "SINGULAR VALUES",
1340 FOR J=1 TO N
1350 PRINT Z(J);
1400 NEXT J
1405 PRINT
1410 REM U in A, V in T, S in Z
1420 PRINT "U"
1422 FOR I=1 TO M
1424 FOR J=1 TO N
1426   PRINT A(I,J);
1428 NEXT J
1430 PRINT
1432 NEXT I
1440 PRINT "V"
1442 FOR I=1 TO M
1444 FOR J=1 TO N
1446   PRINT A(I,J);
1448 NEXT J
1450 PRINT
1450 NEXT I
1600 LET Q=1E-20
1605 REM UT*B
1610 PRINT "SINGULAR VALUES <=",Q," ARE TAKEN AS 0"
1615 REM PRINT "G:";
1620 FOR J=1 TO N
1630 LET S=0
1640 FOR I=1 TO M
1650  LET S=S+A(I,J)*B(I)
1652 NEXT I
1655 IF (Z(J) > Q) THEN LET G(J)=S/Z(J) ELSE G(J)=0
1666 REM PRINT G(J);
1670 NEXT J
1671 REM PRINT
1680 PRINT "Least squares solution"
1690 FOR I=1 TO N
1700 LET S=0
1710 FOR J=1 TO N
1720 LET S=S+T(I,J)*G(J)
1730 NEXT J
1740 LET X(I)=S
1745 PRINT S;
1750 NEXT I
1760 PRINT
1800 quit